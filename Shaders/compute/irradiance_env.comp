#version 460 core
#extension GL_ARB_shading_language_include : enable
#extension GL_GOOGLE_include_directive : enable

precision mediump float;
precision mediump int;

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform sampler2D sourceEnvTexture;
layout(set = 0, binding = 1, rgba32f) uniform restrict writeonly image2D targetIrradianceTexture;

layout(push_constant) uniform Settings {
	layout(offset = 0) float sampleDelta;
	layout(offset = 4) float maxValue;
}
settings;

#include "common.glsl"

void main() {

	/*  */
	if (any(greaterThan(gl_GlobalInvocationID.xy, imageSize(targetIrradianceTexture)))) {
		return;
	}

	/*	*/
	const ivec2 pixel_coords = ivec2(gl_GlobalInvocationID.xy);
	const vec2 normalized_uv_coords = vec2(pixel_coords) / vec2(imageSize(targetIrradianceTexture));
	// TODO: improve mapping.
	const vec3 direction = equirectangular(normalized_uv_coords + vec2(0.5, 0));

	vec3 irradiance = vec3(0.0);

	vec3 up = vec3(0.0, 1.0, 0.0);
	vec3 right = normalize(cross(up, direction));
	up = normalize(cross(direction, right));

	const float sampleDelta = max(0.01, settings.sampleDelta);

	float nrSamples = 0.0;
	for (float phi = 0.0; phi < 2.0 * PI; phi += sampleDelta) {
		vec3 sub_irradiance = vec3(0);
		uint sub_irradiance_sample_count = 0;

		for (float theta = 0.0; theta < 0.5 * PI; theta += sampleDelta) {
			/* spherical to cartesian (in tangent space)	*/
			const vec3 tangentSample = vec3(sin(theta) * cos(phi), sin(theta) * sin(phi), cos(theta));
			/* tangent space to world	*/
			const vec3 sampleVec = tangentSample.x * right + tangentSample.y * up + tangentSample.z * direction;

			const vec2 panoramic_coordinate = inverse_equirectangular(sampleVec);

			/*	Sample from pre-filter downsampled environment texture.	*/
			sub_irradiance += textureLod(sourceEnvTexture, panoramic_coordinate, 2).rgb * cos(theta) * sin(theta);
			sub_irradiance_sample_count++;
		}
		irradiance += sub_irradiance * (1.0 / float(sub_irradiance_sample_count));
		nrSamples++;
	}

	irradiance = PI * irradiance * (1.0 / float(nrSamples));

	//TODO: fix mapping
	ivec2 coordinate_new = imageSize(targetIrradianceTexture) - pixel_coords;
	coordinate_new.x = pixel_coords.x;

	imageStore(targetIrradianceTexture, coordinate_new, vec4(irradiance, 1));
}